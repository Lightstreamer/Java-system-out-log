plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

wrapper {
    // change here and run "gradlew wrapper" to update (only for local installations)
    gradleVersion '7.3.3'
}

repositories {
    jcenter()
}

dependencies {
	implementation 'com.lightstreamer:ls-log-adapter-java:1.0.2'
}

version = '1.0.2'

java {
 	sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    title "Lightstreamer Java logger $version API Reference"
    options.overview 'src/main/javadoc/overview.html'
}

publishing {
    publications {
        maven(MavenPublication) {
        	groupId = 'com.lightstreamer'
            artifactId = 'java-system-out-log'
            
            from components.java

            pom {
                name = 'Lightstreamer Java Logger'
                description = 'Lightstreamer Java Logger'
                url = 'http://www.lightstreamer.com'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                developers {
                    developer {
                        name = 'support'
                        email = 'support@lightstreamer.com'
                        organization = 'Lightstreamer'
                        organizationUrl = 'https://www.lightstreamer.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/Lightstreamer/Java-system-out-log.git"
                    developerConnection = "scm:git:https://github.com/Lightstreamer/Java-system-out-log.git"
                    url = 'https://github.com/Lightstreamer/Java-system-out-log'
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
